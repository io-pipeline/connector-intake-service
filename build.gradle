plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
}

dependencies {
    // Use published BOM from Maven Local
    implementation platform('io.pipeline:pipeline-bom:1.0.0-SNAPSHOT')

    // Core Quarkus dependencies
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-grpc'
    implementation 'io.grpc:grpc-services'  // gRPC health and reflection services
    implementation 'io.quarkus:quarkus-hibernate-orm-panache'
    implementation 'io.quarkus:quarkus-jdbc-mysql'
    implementation 'io.quarkus:quarkus-flyway'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-openapi'

    // Service discovery and registration (matches connector-service baseline)
    implementation 'io.quarkus:quarkus-smallrye-stork'
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'

    // Pipeline libraries (published to Maven Local)
    implementation 'io.pipeline:grpc-stubs:1.0.0-SNAPSHOT'
    implementation 'io.pipeline:dynamic-grpc-registration-clients:1.0.0-SNAPSHOT'
    implementation 'io.pipeline:dynamic-grpc:1.0.0-SNAPSHOT'

    // Utilities
    implementation 'com.google.guava:guava:33.0.0-jre'  // For RateLimiter
    implementation libs.password4j  // For API key verification
    implementation 'com.google.code.gson:gson:2.10.1'  // For JSON processing

    // Testing
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.pipeline:grpc-wiremock:1.0.0-SNAPSHOT'
    testImplementation 'io.rest-assured:rest-assured'
}

group = 'io.pipeline'
version = '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    maxHeapSize = "10g"
    jvmArgs = ["-Xmx10g", "-XX:MaxMetaspaceSize=1g", "--add-opens", "java.base/java.lang=ALL-UNNAMED"]
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

quarkusDev {
    jvmArgs = ["-Xmx10g", "-XX:MaxMetaspaceSize=1g", "--add-opens", "java.base/java.lang=ALL-UNNAMED"]
}

// Publishing configuration
publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        maven(MavenPublication) { publication ->
            from components.java
            //noinspection GroovyAssignabilityCheck
            artifact(file("${buildDir}/quarkus-app/quarkus-run.jar")) { artifact ->
                artifact.classifier = 'runner'
                artifact.builtBy tasks.named('quarkusBuild')
            }
            pom { pom ->
                name.set('Connector Intake Service')
                description.set('Intake and processing service for connector data in the Pipeline platform')
                url.set('https://github.com/io-pipeline/connector-intake-service')

                licenses {
                    license {
                        name.set('Apache License 2.0')
                        url.set('https://www.apache.org/licenses/LICENSE-2.0')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        email.set('krickert@rokkon.com')
                        name.set('Pipeline Engine Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/io-pipeline/connector-intake-service.git')
                    developerConnection.set('scm:git:ssh://github.com/io-pipeline/connector-intake-service.git')
                    url.set('https://github.com/io-pipeline/connector-intake-service')
                }
            }
        }
    }

    repositories {
        // Publish to Maven Local for development
        mavenLocal()

        // Publish to Reposilite (primary artifact repository)
        maven {
            name = "Reposilite"
            url = uri(version.toString().endsWith('-SNAPSHOT')
                ? "https://maven.rokkon.com/snapshots"
                : "https://maven.rokkon.com/releases")
            credentials {
                username = project.findProperty("reposilite.user") ?: System.getenv("REPOS_USER") ?: "admin"
                password = project.findProperty("reposilite.token") ?: System.getenv("REPOS_PAT")
            }
        }

        // Publish to Gitea Maven registry (backup)
        maven {
            name = "Gitea"
            url = uri("https://git.rokkon.com/api/packages/io-pipeline/maven")
            credentials {
                username = project.findProperty("gitea.user") ?: System.getenv("GIT_USER") ?: "krickert"
                password = project.findProperty("gitea.token") ?: System.getenv("GIT_PAT")
            }
        }
    }
}
